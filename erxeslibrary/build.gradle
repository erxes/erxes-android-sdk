apply plugin: 'com.android.library'
apply plugin: 'com.apollographql.apollo'
apply plugin: 'maven-publish'


afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.github.erxes'
                artifactId = 'final'
                version = '1.0'
            }
            // Creates a Maven publication called “debug”.
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                from components.debug

                groupId = 'com.github.erxes'
                artifactId = 'final-debug'
                version = '1.0'
            }
        }
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


    defaultPublishConfig "debug"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'com.apollographql.apollo:apollo-runtime:2.4.4'
    implementation("com.apollographql.apollo:apollo-rx3-support:2.4.4")
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    implementation('com.github.bumptech.glide:glide:4.9.0@aar') {
        transitive = true
    }
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    implementation 'com.squareup.okhttp3:logging-interceptor:4.5.0'
    implementation 'com.mikepenz:iconics-core:3.0.4@aar'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation 'com.github.stfalcon:frescoimageviewer:0.5.0'
    implementation 'com.facebook.fresco:fresco:1.13.0'


    compileOnly 'org.jetbrains:annotations:13.0'
    testCompileOnly 'org.jetbrains:annotations:13.0'

}

apollo {
    customTypeMapping = [
            "Date": "java.lang.Object",
            "JSON": "com.newmedia.erxeslibrary.helper.Json"
    ]
    service("opens") {
        sourceFolder.set("com.erxes.io/opens")
        rootPackageName.set("com.erxes.io.opens")
    }
    service("saas") {
        sourceFolder.set("com.erxes.io/saas")
        rootPackageName.set("com.erxes.io.saas")
    }
    generateKotlinModels.set(false)
}

